import Mathlib.LinearAlgebra.Matrix.NonsingularInverse
import Mathlib.LinearAlgebra.Matrix.Trace
import Mathlib.Data.NNReal.Basic
import Mathlib.Topology.MetricSpace.Basic
import Mathlib.Data.Matrix.Notation
import Mathlib.RingTheory.PowerSeries.Basic
import Mathlib.Analysis.Normed.Algebra.MatrixExponential
import Mathlib.Data.Matrix.Kronecker
import Mathlib.Data.Matrix.Rank
import Mathlib.LinearAlgebra.Matrix.PosDef
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Data.Real.StarOrdered
import Mathlib.Data.Matrix.Vec
import Mathlib.Analysis.CStarAlgebra.Matrix

/-! # Functions and Operators -/

variable {Œπ : Type*} {R : Type*} {l m n p q r : Type*}

variable [Fintype l] [Fintype m] [Fintype n] [Fintype p] [Fintype q] [Fintype r]

variable [DecidableEq l] [DecidableEq m] [DecidableEq n] [DecidableEq p] [DecidableEq q]
  [DecidableEq r]

open scoped Nat BigOperators Matrix NNReal ENNReal

open Matrix

namespace MatrixCookbook

variable [Field R]

/-! ### Functions and Series -/


/-! #### Finite Series -/


/-- The pdf does not mention `hx`! -/
theorem eq_487 (X : Matrix m m R) (n : ‚Ñï) (hx : (X - 1).det ‚â† 0) :
    (X ^ n - 1) * (X - 1)‚Åª¬π = ‚àë i ‚àà Finset.range n, X ^ i := by
  rw [‚Üê geom_sum_mul X n, mul_nonsing_inv_cancel_right _ _ hx.isUnit]

/-! #### Taylor Expansion of Scalar Function -/


/-! #### Matrix Functions by Infinite Series -/


/-! #### Identity and commutations -/


/-! #### Exponential Matrix Function -/

section
open NormedSpace

theorem eq_494 (A : Matrix n n ‚Ñù) : exp ‚Ñù A = ‚àë' n : ‚Ñï, (n !‚Åª¬π : ‚Ñù) ‚Ä¢ A ^ n := by rw [exp_eq_tsum]

theorem eq_495 (A : Matrix n n ‚Ñù) : exp ‚Ñù (-A) = ‚àë' n : ‚Ñï, (n !‚Åª¬π : ‚Ñù) ‚Ä¢ (-1 : Matrix _ _ ‚Ñù) ^ n * A ^ n := by
  simp_rw [exp_eq_tsum, neg_pow A, smul_mul_assoc]

theorem eq_496 (t : ‚Ñù) (A : Matrix n n ‚Ñù) : exp ‚Ñù (t ‚Ä¢ A) = ‚àë' n : ‚Ñï, (n !‚Åª¬π : ‚Ñù) ‚Ä¢ t ^ (n : ‚Ñï) ‚Ä¢ A ^ n := by simp_rw [exp_eq_tsum, smul_pow]

theorem eq_498 (A B : Matrix n n ‚Ñù) (h : A * B = B * A) : exp ‚Ñù A * exp ‚Ñù B = exp ‚Ñù (A + B) :=
  (exp_add_of_commute _ _ _ h).symm

theorem eq_499 (A : Matrix n n ‚Ñù) : (exp ‚Ñù A)‚Åª¬π = exp ‚Ñù (-A) :=
  (exp_neg _ _).symm

end

/-! ### Kronecker and vec Operator -/


/-! #### The Kronecker Product -/


open scoped Kronecker

theorem eq_505 (A : Matrix m n R) (B : Matrix r q R) :
    A ‚äó‚Çñ B = Matrix.of fun i j : _ √ó _ => (A i.1 j.1 ‚Ä¢ B) i.2 j.2 :=
  rfl

theorem eq_506 (A : Matrix m n R) (B C : Matrix r q R) : A ‚äó‚Çñ (B + C) = A ‚äó‚Çñ B + A ‚äó‚Çñ C :=
  kronecker_add _ _ _

theorem eq_507 [Nontrivial m] [Nonempty n] :
    ¬¨‚àÄ (A : Matrix m n R) (B : Matrix m n R), A ‚äó‚Çñ B = B ‚äó‚Çñ A := by
  intro h
  obtain ‚ü®m1, m2, hm‚ü© := exists_pair_ne m
  obtain ‚ü®n1‚ü© := id ‚ÄπNonempty n‚Ä∫
  let A := single m1 n1 (1 : R)
  let B := single m2 n1 (1 : R)
  have := Matrix.ext_iff.mpr (h A B) (m1, m2) (n1, n1)
  simp [single_apply_same, single_apply_of_row_ne hm,
    mul_zero, mul_one, one_ne_zero, A, B] at this

/-- Note we have to "cast" between the types -/
theorem eq_508 (A : Matrix m n R) (B : Matrix r q R) (C : Matrix l p R) :
    A ‚äó‚Çñ (B ‚äó‚Çñ C) = reindex (Equiv.prodAssoc _ _ _) (Equiv.prodAssoc _ _ _) (A ‚äó‚Çñ B ‚äó‚Çñ C) :=
  (kronecker_assoc _ _ _).symm

theorem eq_509 (a b : R) (A : Matrix m n R) (B : Matrix r q R) :
    (a ‚Ä¢ A) ‚äó‚Çñ (b ‚Ä¢ B) = (a * b) ‚Ä¢ A ‚äó‚Çñ B := by rw [smul_kronecker, kronecker_smul, smul_smul]

theorem eq_510 (A : Matrix m n R) (B : Matrix r q R) : (A ‚äó‚Çñ B)·µÄ = A·µÄ ‚äó‚Çñ B·µÄ := by
  rw [kroneckerMap_transpose]

theorem eq_511 (A : Matrix l m R) (B : Matrix p q R) (C : Matrix m n R) (D : Matrix q r R) :
    A ‚äó‚Çñ B * C ‚äó‚Çñ D = (A * C) ‚äó‚Çñ (B * D) := by rw [Matrix.mul_kronecker_mul]

theorem eq_512 (A : Matrix m m R) (B : Matrix n n R) : (A ‚äó‚Çñ B)‚Åª¬π = A‚Åª¬π ‚äó‚Çñ B‚Åª¬π :=
  inv_kronecker _ _

lemma eq_513 : (sorry : Prop) := sorry

lemma eq_514 (A : Matrix l m R) (B : Matrix p q R) : rank (A ‚äó‚Çñ B) = rank A * rank B := sorry

theorem eq_515 (A : Matrix m m R) (B : Matrix n n R) : trace (A ‚äó‚Çñ B) = trace A * trace B := by
  simp_rw [Matrix.trace, Matrix.diag, Finset.sum_mul, Finset.mul_sum, ‚Üê Finset.univ_product_univ,
    Finset.sum_product, kronecker_apply]

theorem eq_516 (A : Matrix m m R) (B : Matrix n n R) :
    det (A ‚äó‚Çñ B) = det A ^ Fintype.card n * det B ^ Fintype.card m :=
  det_kronecker _ _

lemma eq_517 : (sorry : Prop) := sorry
lemma eq_518 : (sorry : Prop) := sorry
lemma eq_519 : (sorry : Prop) := sorry

/-! #### The Vec Operator -/

theorem eq_520 (A : Matrix l m R) (X : Matrix m n R) (B : Matrix n p R) :
    vec (A * X * B) = B·µÄ ‚äó‚Çñ A *·µ• vec X := by
  rw [kronecker_mulVec_vec, transpose_transpose]

theorem eq_521 (A B : Matrix m n R) : (A·µÄ * B).trace = (vec A ‚¨ù·µ• vec B) :=
  (vec_dotProduct_vec _ _).symm

theorem eq_522 (A B : Matrix m n R) : vec (A + B) = vec A + vec B := vec_add _ _

theorem eq_523 (r : R) (A : Matrix m n R) : vec (r ‚Ä¢ A) = r ‚Ä¢ vec A := vec_smul _ _

-- note: `B·µÄ` is `B` in the PDF
theorem eq_524 (a : m ‚Üí R) (X : Matrix m n R) (B : Matrix n n R) (c : m ‚Üí R) :
    a ·µ•* (X * B * X·µÄ) ‚¨ù·µ• c = vec X ·µ•* (B·µÄ ‚äó‚Çñ vecMulVec c a) ‚¨ù·µ• vec X := by
  -- not the proof from the book
  simp only [vec, dotProduct, vecMul, Matrix.mul_apply, Finset.sum_mul, Finset.mul_sum, Matrix.kroneckerMap_apply,
    transpose_apply, Matrix.replicateRow_apply, Matrix.replicateCol_apply, Fintype.sum_unique]
  simp_rw [‚Üê Finset.univ_product_univ, Finset.sum_product, @Finset.sum_comm n _ m, vecMulVec,
    of_apply]
  rw [Finset.sum_comm]
  refine Finset.sum_congr rfl fun k _ => ?_
  refine Finset.sum_congr rfl fun l _ => ?_
  rw [Finset.sum_comm]
  refine Finset.sum_congr rfl fun kk _ => ?_
  refine Finset.sum_congr rfl fun ll _ => ?_
  ring

/-! ### Vector Norms -/


/-! #### Examples -/


theorem eq_525 (x : n ‚Üí ‚ÑÇ) : ‚Äñ(WithLp.equiv 1 _).symm x‚Äñ = ‚àë i, ‚Äñx i‚Äñ := by
  simpa using PiLp.norm_eq_of_nat 1 Nat.cast_one.symm ((WithLp.equiv 1 _).symm x)

theorem eq_526 (x : n ‚Üí ‚ÑÇ) : ‚Üë(‚Äñ(WithLp.equiv 2 _).symm x‚Äñ ^ 2 : ‚Ñù) = star x ‚¨ù·µ• x := by
  rw [dotProduct_comm, ‚Üê EuclideanSpace.inner_toLp_toLp, inner_self_eq_norm_sq_to_K,
    Complex.ofReal_pow]
  rfl  -- porting note: added

theorem eq_527 (x : n ‚Üí ‚ÑÇ) (p : ‚Ñù‚â•0‚àû) (h : 0 < p.toReal) :
    ‚ÄñWithLp.toLp p x‚Äñ = (‚àë i, ‚Äñx i‚Äñ ^ p.toReal) ^ (1 / p.toReal) := by
  simp_rw [PiLp.norm_eq_sum h, PiLp.toLp_apply]

theorem eq_528 (x : n ‚Üí ‚ÑÇ) : ‚ÄñWithLp.toLp ‚àû x‚Äñ = ‚®Ü i, ‚Äñx i‚Äñ := by
  simp_rw [PiLp.norm_eq_ciSup, PiLp.toLp_apply]

/-! ### Matrix Norms -/


/-! #### Definitions -/


section
open scoped Matrix.Norms.Elementwise

theorem eq_529 (A : Matrix n n ‚Ñù) : 0 ‚â§ ‚ÄñA‚Äñ :=
  norm_nonneg _

theorem eq_530 (A : Matrix n n ‚Ñù) : ‚ÄñA‚Äñ = 0 ‚Üî A = 0 :=
  norm_eq_zero

theorem eq_531 (r : ‚Ñù) (A : Matrix n n ‚Ñù) : ‚Äñr ‚Ä¢ A‚Äñ = |r| * ‚ÄñA‚Äñ := by
  rw [norm_smul r A, Real.norm_eq_abs]

theorem eq_532 (A B : Matrix n n ‚Ñù) : ‚ÄñA + B‚Äñ ‚â§ ‚ÄñA‚Äñ + ‚ÄñB‚Äñ :=
  norm_add_le _ _

end

/-! #### Induced Norm or Operator Norm -/


-- we just use one of the norms as an example; there is no generalization available
section

attribute [local instance] Matrix.linftyOpNormedAddCommGroup Matrix.linftyOpNormedSpace

attribute [local instance] Matrix.linftyOpNormedRing Matrix.linftyOpNormedAlgebra

theorem ContinuousLinearMap.sSup_sphere_eq_norm {ùïú ùïú‚ÇÇ E F : Type*}
    [NormedAddCommGroup E] [SeminormedAddCommGroup F]
    [DenselyNormedField ùïú] [NontriviallyNormedField ùïú‚ÇÇ] {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ}
    [NormedAlgebra ‚Ñù ùïú] [NormedSpace ùïú E]
    [NormedSpace ùïú‚ÇÇ F] [RingHomIsometric œÉ‚ÇÅ‚ÇÇ] (f : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F) :
    sSup ((fun x => ‚Äñf x‚Äñ) '' Metric.sphere 0 1) = ‚Äñf‚Äñ := by
  simpa only [NNReal.coe_sSup, Set.image_image] using NNReal.coe_inj.2 f.sSup_sphere_eq_nnnorm

lemma eq_533 (A : Matrix m n ‚Ñù) : ‚ÄñA‚Äñ = sSup { ‚ÄñA.mulVec x‚Äñ | (x) (hx : ‚Äñx‚Äñ = 1)} := by
  suffices ‚ÄñA‚Äñ = sSup ((‚ÄñA.mulVec ¬∑‚Äñ) '' Metric.sphere 0 1) by
    simpa [Set.image, mem_sphere_zero_iff_norm] using this
  simp_rw [linfty_opNorm_eq_opNorm, ‚Üê ContinuousLinearMap.sSup_sphere_eq_norm]
  simp

theorem eq_534 [Nonempty n] : ‚Äñ(1 : Matrix n n ‚Ñù)‚Äñ = 1 :=
  norm_one

theorem eq_535 (A : Matrix m n ‚Ñù) (x : n ‚Üí ‚Ñù) : ‚ÄñA.mulVec x‚Äñ ‚â§ ‚ÄñA‚Äñ * ‚Äñx‚Äñ :=
  linfty_opNorm_mulVec _ _

theorem eq_536 (A : Matrix m n ‚Ñù) (B : Matrix n p ‚Ñù) : ‚ÄñA * B‚Äñ ‚â§ ‚ÄñA‚Äñ * ‚ÄñB‚Äñ :=
  linfty_opNorm_mul _ _

end

/-! #### Examples -/


lemma eq_537 (A : Matrix m n ‚Ñù) : sorry = ‚®Ü j, ‚àë i, ‚ÄñA i j‚Äñ := sorry

lemma eq_538 (A : Matrix m n ‚Ñù) :
    (open scoped Matrix.Norms.L2Operator in ‚ÄñA‚Äñ) =
      NNReal.sqrt (Finset.univ.sup fun i => ‚ü®_, A.eigenvalues_conjTranspose_mul_self_nonneg i‚ü©) := sorry

lemma eq_539 : (sorry : Prop) := sorry

theorem eq_540 (A : Matrix m n ‚Ñù) :
    (open scoped Matrix.Norms.Operator in ‚ÄñA‚Äñ) =
      ‚Üë(Finset.univ.sup fun i => ‚àë j, ‚ÄñA i j‚Äñ‚Çä) := by
  rw [linfty_opNorm_def]

theorem eq_541 (A : Matrix m n ‚Ñù) :
    (open scoped Matrix.Norms.Frobenius in ‚ÄñA‚Äñ) =
      Real.sqrt (‚àë i, ‚àë j, ‚ÄñA i j‚Äñ ^ (2 : ‚Ñù)) := by
  rw [frobenius_norm_def, Real.sqrt_eq_rpow]

theorem eq_542 (A : Matrix m n ‚Ñù) :
    (open scoped Matrix.Norms.Elementwise in ‚ÄñA‚Äñ) =
      ‚Üë(Finset.univ.sup fun ij : _ √ó _ => ‚ÄñA ij.1 ij.2‚Äñ‚Çä) := by
  simp_rw [‚Üê Finset.univ_product_univ, Finset.sup_product_left, ‚Üê Pi.nnnorm_def, coe_nnnorm]

lemma eq_543 : (sorry : Prop) := sorry

/-! #### Inequalities -/

-- TODO: are the bounds tight?
lemma eq_544 (A : Matrix m n ‚Ñù) :
    letI m' := Fintype.card m
    letI n' := Fintype.card n
    letI d := A.rank
    let norms : Fin 6 ‚Üí (Matrix m n ‚Ñù ‚Üí ‚Ñù) := ![
      open scoped Norms.Elementwise in (‚Äñ¬∑‚Äñ),
      sorry, -- L1 norm
      open scoped Norms.Operator in (‚Äñ¬∑‚Äñ),
      open scoped Norms.L2Operator in (‚Äñ¬∑‚Äñ),
      open scoped Norms.Frobenius in (‚Äñ¬∑‚Äñ),
      sorry -- Ky Fan norm
    ]
    let coeffs : Matrix _ _ ‚Ñù := !![
      1,                   1,              1,              1,        1,        1;
      m',                  1,              m',             .sqrt m', .sqrt m', .sqrt m';
      n',                  n',             1,              .sqrt n', .sqrt n', .sqrt n';
      .sqrt (m' * n'),     .sqrt n',       .sqrt m',       1,        1,        1;
      .sqrt (m' * n'),     .sqrt n',       .sqrt m',       .sqrt d,  1,        1;
      .sqrt (m' * n' * d), .sqrt (n' * d), .sqrt (m' * d), d,        .sqrt d,  1]
    ‚àÄ i j, norms i A ‚â§ coeffs i j * norms j A := by
  -- unpack the matrix into the elements
  simp only [Fin.forall_fin_succ, IsEmpty.forall_iff, and_true, Matrix.of_apply, Fin.reduceSucc,
    cons_val, one_mul]
  refine ‚ü®?_, ?_, ?_, ?_, ?_, ?_‚ü©
  -- the diagonal elements are easy, but 30 missing lemmas remain
  ¬∑ refine ‚ü®le_rfl, sorry, sorry, sorry, sorry, sorry‚ü©
  ¬∑ refine ‚ü®sorry, le_rfl, sorry, sorry, sorry, sorry‚ü©
  ¬∑ refine ‚ü®sorry, sorry, le_rfl, sorry, sorry, sorry‚ü©
  ¬∑ refine ‚ü®sorry, sorry, sorry, le_rfl, sorry, sorry‚ü©
  ¬∑ refine ‚ü®sorry, sorry, sorry, sorry, le_rfl, sorry‚ü©
  ¬∑ refine ‚ü®sorry, sorry, sorry, sorry, sorry, le_rfl‚ü©



/-! #### Condition Number -/

open scoped Matrix.Norms.L2Operator in
lemma eq_545 (A : Matrix n n ‚Ñù) : ‚ÄñA‚Äñ * ‚ÄñA‚Åª¬π‚Äñ = sorry / sorry := sorry

/-! ### Rank -/

/-! #### Sylvester‚Äôs Inequality -/

theorem eq_546 (A : Matrix m n ‚Ñù) (B : Matrix n r ‚Ñù) :
    rank A + rank B - Fintype.card n ‚â§ rank (A * B) ‚àß rank (A * B) ‚â§ min (rank A) (rank B) :=
  ‚ü®sorry, rank_mul_le _ _‚ü©

/-! ### Integral Involving Dirac Delta Functions -/


/-! ### Miscellaneous -/


lemma eq_547 : (sorry : Prop) := sorry
lemma eq_548 : (sorry : Prop) := sorry

theorem eq_549 (A : Matrix m n ‚Ñù) :
    A.rank = A·µÄ.rank ‚àß A.rank = (A * A·µÄ).rank ‚àß A.rank = (A·µÄ * A).rank :=
  ‚ü®A.rank_transpose.symm, A.rank_self_mul_transpose.symm, A.rank_transpose_mul_self.symm‚ü©

theorem eq_550 (A : Matrix m m ‚Ñù) :
    A.PosDef ‚Üî ‚àÉ B : Matrix m m ‚Ñù, IsUnit B ‚àß A = B * B·µÄ := by
  rw [PosDef.posDef_iff_eq_conjTranspose_mul_self,
    (Function.RightInverse.surjective transpose_transpose).exists]
  simp [isUnit_transpose]

end MatrixCookbook
